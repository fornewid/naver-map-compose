plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.vanniktech.maven.publish'
    id 'org.jetbrains.dokka'
    id 'me.tylerbwong.gradle.metalava'
}

android {
    namespace 'com.naver.maps.map.compose'
    compileSdkVersion buildConfig.compileSdk
    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${libs.versions.composeCompiler.get()}"
    }
    buildFeatures {
        buildConfig false
        compose true
    }
    kotlinOptions {
        freeCompilerArgs += '-Xexplicit-api=warning'
    }
    // https://github.com/robolectric/robolectric-gradle-plugin/issues/144#issuecomment-265620470
    testOptions.unitTests.all {
        jvmArgs '-noverify'
    }
}

metalava {
    sourcePaths.setFrom("src/main")
    filename.set("api/current.api")
    reportLintsAsErrors.set(true)
}

dependencies {
    api libs.naver.map
    api libs.naver.map.location

    implementation libs.compose.foundation
    implementation libs.kotlin.stdlib

    // To provide FusedLocationSource
    implementation libs.google.play.services.location
    implementation libs.accompanist.permissions

    testImplementation libs.kotlin.test
    testImplementation libs.kotlin.reflect
    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.androidx.test.junit
}
