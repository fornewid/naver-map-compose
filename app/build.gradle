plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion buildConfig.compileSdk
    defaultConfig {
        applicationId "com.naver.maps.map.compose.demo"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 1_02_00
        versionName "1.2.1-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        // We use a bundled debug keystore, to allow debug builds from CI to be upgradable
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${libs.versions.composeCompiler.get()}"
    }
    buildFeatures {
        compose true
    }
    packagingOptions {
        // Multiple dependencies bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {
    implementation project(':naver-map-compose')
    implementation libs.google.play.services.location
    implementation libs.naver.map.clustering

    implementation libs.compose.foundation
    implementation libs.compose.material
    implementation libs.compose.materialIconsExtended
    implementation libs.compose.navigation
    implementation libs.compose.activity
    implementation libs.androidx.core
    implementation libs.material.material
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core
}
