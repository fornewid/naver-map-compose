plugins {
    id("navermap.android.application")
    id("navermap.android.compose")
}

android {
    namespace 'com.naver.maps.map.compose.demo'
    defaultConfig {
        applicationId "com.naver.maps.map.compose.demo"
        versionCode 1
        versionName "1.0.0"

        def localProperties = new Properties()
        def localPropertiesFile = rootProject.file("local.properties")
        if (localPropertiesFile.exists()) {
            localProperties.load(new FileInputStream(localPropertiesFile))
        }

        manifestPlaceholders = [NCP_KEY_ID: localProperties.getProperty("NCP_KEY_ID", "DEFAULT_API_KEY")]
    }
    signingConfigs {
        // We use a bundled debug keystore, to allow debug builds from CI to be upgradable
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/AL2.0', '/META-INF/LGPL2.1']
        }
    }
}

dependencies {
    implementation project(':naver-map-compose')
    implementation libs.naver.map.tedclustering

    implementation libs.compose.foundation
    implementation libs.compose.material
    implementation libs.compose.materialIconsExtended
    implementation libs.compose.navigation
    implementation libs.compose.activity
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core
}
